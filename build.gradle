plugins {
    id 'java'
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '1.3.10'
}

group 'cn.yiiguxing.tool.dcmviewer'
version = "${version}${buildNumber ? '-' + buildNumber : ''}"
mainClassName = 'cn.yiiguxing.tool.dcmviewer.DicomViewerKt'

repositories {
    maven { url = 'https://maven.aliyun.com/repository/public' }
    maven { url = 'http://www.dcm4che.org/maven2/' }
    jcenter()
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile group: 'javax.vecmath', name: 'vecmath', version: '1.5.2'
    compile group: 'org.dcm4che', name: 'dcm4che-imageio', version: '5.14.1'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

sourceCompatibility = javaVersion
targetCompatibility = javaTargetVersion

[compileKotlin, compileTestKotlin]*.kotlinOptions {
    jvmTarget = javaTargetVersion
}


jar {
    from configurations.runtime.asFileTree.files.collect { zipTree(it) }
    from(configurations.runtime) {
        into 'lib'
    }
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
